plugins {
    id 'java'
}

group 'com.example'
version '1.0-SNAPSHOT'

targetCompatibility = 8
sourceCompatibility = 8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

configurations {
    // give tests access to annotationProcessor dependencies
    testImplementation.extendsFrom annotationProcessor
}

dependencies {
    implementation "systems.manifold:manifold-graphql-rt:${manifoldVersion}"
    testImplementation 'junit:junit:4.12'
    // Add manifold to -processorpath for javac
    annotationProcessor "systems.manifold:manifold-graphql:${manifoldVersion}"
}

tasks.withType(JavaCompile) {
    if (JavaVersion.current() != JavaVersion.VERSION_1_8 &&
            sourceSets.main.allJava.files.any { it.name == "module-info.java" }) {
        // if you DO define a module-info.java file:
        options.compilerArgs += ['-Xplugin:Manifold', '--module-path', it.classpath.asPath]
    } else {
        // if you DO NOT define a module-info.java file:
        options.compilerArgs += ['-Xplugin:Manifold']
    }
    // compile .graphql files
    options.compilerArgs += ['-Amanifold.source.graphql=.*']
}